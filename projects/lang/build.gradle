plugins {
    id 'java-library'
    id 'me.champeau.jmh' version '0.7.2'
    id 'io.morethan.jmhreport' version '0.9.0'
}

description "Essential value types"

sourceSets {
    lab {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    jmh 'org.openjdk.jmh:jmh-core:1.37'
    jmh 'org.scala-lang:scala-library:2.13.8'
    jmh 'org.typelevel:spire_3:0.18.0'
    jmh 'org.apfloat:apfloat:1.14.0'
    jmh 'org.jscience:jscience:4.3.1'
    jmh 'org.clojure:clojure:1.12.0'
    jmh 'org.clojure:math.numeric-tower:0.1.0'
    jmh 'org.libj:math:0.6.8'
    jmh 'org.huldra.math:bigint:0.7.1'
    jmh 'com.google.guava:guava:33.4.0-jre'
    jmhRuntimeOnly 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.7.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    
    labImplementation 'org.openjdk.jol:jol-core:0.17'
}

jmhJar {
    duplicatesStrategy = DuplicatesStrategy.WARN
}

String jmhIncludesPropertyValue = findProperty("jmhIncludes")
jmh {
    if (jmhIncludesPropertyValue != null) {
        includes = jmhIncludesPropertyValue.split(",") as List
    }
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
    resultFormat = 'JSON'
}

def jmhReportHtmlDir = 'build/results/jmh/html'
project.tasks.getByName("jmhReport").doFirst {
    project.file(jmhReportHtmlDir).mkdirs()
}
jmhReport {
    jmhResultPath = project.file('build/results/jmh/results.json')
    jmhReportOutput = project.file(jmhReportHtmlDir)
}

import java.lang.reflect.Modifier
task listPublicMethods {
    doLast {
        def className = findProperty("listClassName")
        def urls = sourceSets.jmh.runtimeClasspath.collect { it.toURI().toURL() } as URL[]
        def classLoader = new URLClassLoader(urls, getClass().classLoader)
        def clazz = classLoader.loadClass(className)
        // List only public methods declared in the class (excluding inherited ones):
        clazz.declaredMethods.findAll { Modifier.isPublic(it.modifiers) }.each { println it }
        // Alternatively, to list all public methods (including inherited ones), use:
        // clazz.methods.each { println it }
    }
}
